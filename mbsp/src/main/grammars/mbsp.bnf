/*
 * Copyright (C) 2019-2021 cofe
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

{
  classHeader="gpl.txt"

  psiImplUtilClass="tk.cofe.plugin.mbsp.psi.impl.MbspPsiUtil"

  parserClass="tk.cofe.plugin.mbsp.parser.MbspParser"

  implements="tk.cofe.plugin.mbsp.psi.MbspPsiCompositeElement"
  extends="tk.cofe.plugin.mbsp.psi.impl.MbspPsiCompositeElementBase"

  psiClassPrefix="Mbsp"
  psiImplClassSuffix="Impl"
  psiPackage="tk.cofe.plugin.mbsp.psi"
  psiImplPackage="tk.cofe.plugin.mbsp.psi.impl"

  elementTypeHolderClass="tk.cofe.plugin.mbsp.MbspTypes"
  elementTypePrefix=""
  elementTypeClass="tk.cofe.plugin.mbsp.psi.MbspElementType"
  tokenTypeClass="tk.cofe.plugin.mbsp.psi.MbspTokenType"

  extends(".*(Expression|Literal)")="expression"
  tokens=[
    EXPRESSION_START="regexp:[$#]\{"
//    EXPRESSION_END="}"

    DOT="."
    COMMA=","

    LPARENTH="("
    RPARENTH=")"
    LBRACKET="["
    RBRACKET="]"
    LBRACE="{"
    RBRACE="}"

    PLUS="+"
    MINUS="-"
    MULTIPLY="*"
    DIVISION="/"
    MODULO="%"

    NEGATE="!"
    NOT_EQUAL="!="
    EQUAL="=="
    EQ="="
    LESS="<"
    LESS_EQUAL="<="
    GREATER=">"
    GREATER_EQUAL=">="

    EQ_KEYWORD="eq"
    NEQ_KEYWORD="neq"
    LT_KEYWORD="lt"
    LT_EQ_KEYWORD="lte"
    GT_KEYWORD="gt"
    GT_EQ_KEYWORD="gte"

    AND_AND="&&"
    OR_OR="||"
    AND_KEYWORD="and"
    OR_KEYWORD="or"
    NOT_KEYWORD="not"

    OR="|"
    XOR="^"
    AND="&"
    NOT="~"

    BAND_KEYWORD="band"
    BOR_KEYWORD="bor"
    XOR_KEYWORD="xor"

    NEW_KEYWORD="new"
    TRUE_KEYWORD="true"
    FALSE_KEYWORD="false"
    NULL_KEYWORD="null"

    INSTANCEOF_KEYWORD="instanceof"
    NOT_IN_KEYWORD="not in"
    IN_KEYWORD="in"

    SHIFT_LEFT="<<"
    SHIFT_RIGHT=">>"
    SHIFT_RIGHT_LOGICAL=">>>"

    SHIFT_LEFT_KEYWORD="shl"
    SHIFT_RIGHT_KEYWORD="shr"
    SHIFT_RIGHT_LOGICAL_KEYWORD="ushr"

    AT_KEYWORD="@"
    DOLLAR_KEYWORD="$"

    MODE_KEYWORD='mode'
    JAVA_TYPE_KEYWORD='javaType'
    JDBC_TYPE_KEYWORD='jdbcType'
    JDBC_TYPE_KEYWORD_NAME='jdbcTypeName'
    NUMERIC_SCALE_KEYWORD='numericScale'
    TYPE_HANDLER_KEYWORD='typeHandler'
    RESULT_MAP_KEYWORD='resultMap'

    QUESTION="?"
    COLON=":"

    WHITE_SPACE_CHAR='regexp:\p{Space}+'

    INTEGER_LITERAL="regexp:\p{Digit}*" // [0-9]
    DOUBLE_LITERAL="regexp:\p{Digit}+\.\p{Digit}+" // [0-9]+\.[0-9]+

    CHARACTER_LITERAL="regexp:'[^\r\n]*'"
    STRING_LITERAL="regexp:\"[^\r\n]*\""

    IDENTIFIER="regexp:\p{javaJavaIdentifierStart}\p{javaJavaIdentifierPart}*"
  ]
}

root ::= EXPRESSION_START rootElement RBRACE {pin=1}
private rootRecover ::= !(RBRACE)
private rootElement ::= expression {
    recoverWhile="rootRecover"
}

expression ::= unaryExpression |
            referenceExpression |
            parenthesizedExpression |
            conditionalExpression |
            binaryExpression |
            methodCallExpression |
            indexedExpression |
            variableExpression |
            literalExpression |
            paramConfigExpression
{
  methods=[
    getType
  ]
}

private bitwiseOperations ::= '!' | '~'
private unaryOperator ::= bitwiseOperations |
                          '+' | '-' | 'not'
unaryExpression ::= unaryOperator expression {
  methods=[
    getUnaryOperator
  ]
}

projectionExpression ::= '{' expression '}'
{
  methods=[
    projectionExpression="expression"
  ]
}
selectionExpression ::= '{' ('?' | '^' | '$') expression '}'
{
  methods=[
    selectorExpression="expression"
  ]
}

referenceExpression ::= (variableExpression | ('@')? IDENTIFIER)
                        ('.' IDENTIFIER)* ('@' IDENTIFIER)?
                        ('.'  selectionExpression)?
                        ('.'  projectionExpression )?
{
   mixin="tk.cofe.plugin.mbsp.psi.impl.MbspReferenceExpressionBase"
   methods=[getReferences]
}

parenthesizedExpression ::= LPARENTH expression RPARENTH { pin=1 }

private conditionalExpressionTail ::= ':' expression {pin=1}
conditionalExpression ::= expression '?' expression conditionalExpressionTail {
  methods=[
    expression=""
    condition="expression[0]"
    then="expression[1]"
    else="expression[2]"
  ]
}

// binary ----------------------------------------------------------------
private plusMinusOperations ::= '+' | '-'
private divideMultiplyOperations ::=  '*' | '/' | '%'
private booleanOperations ::= "&&" | "||" |
                              "and" | "or" |
                              "not"
private bitwiseBooleanOperations ::=  "|" | "^" | "&" | "band" | "bor" | "xor"
private equalityOperations ::= "==" | "!=" |
                               "eq" | "neq"
private relationalOperations ::= '<'  | "<="  | '>'  | ">=" |
                                 "lt" | "lte" | "gt" | "gte"
private shiftOperations ::= "<<" | ">>" | ">>>" |
                            "shl" | "shr" | "ushr"
private instanceOfOperation ::= "instanceof"
private setOperations ::= "not in" | "in"
private binaryOperations ::=
                           plusMinusOperations |
                           divideMultiplyOperations |
                           bitwiseBooleanOperations |
                           instanceOfOperation |
                           shiftOperations |
                           booleanOperations |
                           equalityOperations |
                           relationalOperations |
                           setOperations
                           { name="operator" }
binaryExpression ::= expression binaryOperations expression {
  methods=[
    expression=""
    left="expression[0]"
    right="expression[1]"
    getOperator
  ]
}

parameterList ::= expression? (',' expression)* {
  methods=[
    parametersList="expression"
    getParameterCount
  ]
}
methodCallExpression ::= referenceExpression '(' parameterList ')'  {
  methods=[
    method="expression"
  ]
  pin(".*")="'\('"
}

indexedExpression ::= (referenceExpression | variableExpression) '[' expression ']'
                      ('.' IDENTIFIER)*
                      ('.'  selectionExpression | projectionExpression)? {
  methods=[
    expression=""
    referenceQualifier="expression[0]"
    indexExpression="expression[1]"
  ]
  pin=2
}

variableExpression ::= '#' IDENTIFIER {
  mixin="tk.cofe.plugin.mbsp.psi.impl.MbspVariableExpressionBase"
}

private numberLiteralExpression ::= INTEGER_LITERAL | DOUBLE_LITERAL
private textLiteralExpression ::= STRING_LITERAL | CHARACTER_LITERAL
private booleanLiteralExpression ::= TRUE_KEYWORD | FALSE_KEYWORD
literalExpression ::= numberLiteralExpression |
                      textLiteralExpression |
                      booleanLiteralExpression |
                      NULL_KEYWORD {
  methods=[
    getConstantValue
  ]
}

private classNameExpression ::= IDENTIFIER ('.' IDENTIFIER)*
private modeExpression ::= 'mode' '=' IDENTIFIER {
  pin = 1
}
private javaTypeExpression ::= 'javaType' '=' classNameExpression {
  pin = 1
}
private jdbcTypeExpression ::= 'jdbcType' '=' IDENTIFIER {
  pin = 1
}
private jdbcTypeNameExpression ::= 'jdbcTypeName' '=' IDENTIFIER {
  pin = 1
}
private numericScaleExpression ::= 'numericScale' '=' INTEGER_LITERAL {
  pin = 1
}
private typeHandlerExpression ::= 'typeHandler' '=' classNameExpression {
  pin = 1
}
private resultMapExpression ::= 'resultMap' '=' IDENTIFIER {
  pin = 1
}
paramConfigExpression ::= expression ',' (modeExpression | javaTypeExpression | jdbcTypeExpression | jdbcTypeNameExpression | numericScaleExpression | typeHandlerExpression | resultMapExpression) {
  pin = 2
}